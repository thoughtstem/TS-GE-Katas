#lang scribble/manual

@;{

   Reasons this exists:
   *  If the tech is messed up in some class, these can serve as a fallback
   *  If (for some reasons) there's no DrRacket, or we must use someone else's computers, these katas can work on wescheme.org
   *  If a student needs supplemental material, he or she can be given these katas (to work on [partially] independently)
   *  For need to pivot old curriculum to something that is static and stable
   *  To have a curriculum that does truly try to teach computational thinking and coding skills in a more fundamental (less flashy) way
   *  For supplemental teacher training...
 
}

@title{Coding Fundamentals}

This is a set of core katas that will prepare students for any ThoughtSTEM
class.  They are designed to give students a strong foundation in computer
science and computational thinking.  

Note that unlike many of our classes, these katas are not particularly
"flashy".  They don't involve games, or robots, or virtual reality.

On the other hand, they are designed to strengthen skills that will help
students excel in any domain -- whether it's games, or robotics, or virtual
reality, or anything else really.

These katas are intended to be a fun, enjoyable, visually appealing way of
strengthening computational problem solving skills.

@(require 2htdp/image)

Kata key:

@(define (meta-kata)                                     (circle 10 'outline 'black))
@(define (shape-kata)                                    (circle 10 'solid 'orange))
@(define (combo-kata)                                    (circle 10 'solid 'green))
@(define (picture-project)                               (circle 10 'solid 'pink))

@;@(define (text-image-kata)                               (circle 10 'solid 'black))
@;@(define (meme-project)                                  (circle 10 'solid 'blue))

@;@(define (basic-composite-image-transformation)          (circle 10 'solid 'red))
@;@(define (intermediate-composite-image-transformation)   (circle 10 'solid 'purple))

@(meta-kata)     
- @bold{Meta Katas} - for skills related to excelling at katas.

@(shape-kata)   
- @bold{Shape Katas} - for skills related to basic shapes.

@(combo-kata)
- @bold{Combo Katas} - for skills related to building combination shapes.

@(picture-project)
- @bold{Picture Project Katas} - for skills related to building more complex combination shapes.

@;@(text-image-kata)     
@;- @bold{Text Image Katas} - for skills related to images of text and typography

@;@(meme-project)
@;- @bold{Meme Project Katas} - for skills related to text, sahpe and external image combinations.

@;@(basic-composite-image-transformation)
@;- @bold{Basic Composite Image Transformations} - for skills related to functions that combine multiple images

@;@(intermediate-composite-image-transformation)
@;- @bold{Intermediate Composite Image Transformations} - for skills related to more complex functions that combine
@;multiple images


@;=== Meta Katas ===

@section{Meta Katas}


@(meta-kata) @bold{Core Values Kata} (1 minute.)

@margin-note*{Hand motions can make these easier to remember and repeat. For
             example: 'It's not about my code.' Point to computer. 'It's about
             what I can code.' Point to brain."}

@margin-note*{Teaching Tip: Depending on the size of your class, you may want to challenge them
             to rattle these off 1 by 1, in pairs or small groups, or as an entire
             class.}

@italic{Can recite the first two ThoughtSTEM core values}

@itemlist[@item{It's not about my code.     It's about what I can code.}
          @item{It's not about me.          It's about my team.}]



@;=== Shape Katas ===

@section{Shape Katas} @; rename "shape" to something more interesting?


@;-------- BRONZE ---------

@(shape-kata)  @bold{Bronze Shape Kata} or @bold{Red Circle Kata} (5 minutes)

@margin-note*{Teaching Tip: Start with all the code projected/written on the board.
              Remove the lines one by one, testing the students each time.}

Within five minutes, the student can create a red circle.

Like so:

@codeblock{
 #lang racket
 (require 2htdp/image)
 (circle 80 'solid 'red)}


@;-------- SILVER ---------

@(shape-kata)  @bold{Silver Shape Kata} or @bold{Any Shape Kata} (5 minutes)

@margin-note*{Teaching Tip: Don't give any example code for this badge.
              Have students extrapolate from circle code.}

Within five minutes, the student can translate a sentence of this type:

@italic{Make a gold star that is 30 pixels.}

To code of this type:

@codeblock{
 #lang racket
 (require 2htdp/image)
 (star 30 'solid 'gold)}

@margin-note*{This badge should ONLY be earned after student has built
              multiple shapes. }

Other shapes to use:
@itemlist[@item{triangle}
          @item{square}
          @item{rectangle *note: needs 2 number arguments}
          @item{ellipse   *note: needs 2 number arguments}]

@;--------- GOLD ---------

@(shape-kata)  @bold{Gold Shape Kata} or @bold{Break-It Shape Kata} (5 minutes)

Within five minutes, the student can code a shape with a color that
@bold{doesn't} work.

NOTE: The code @bold{must still run}, but the shape should appear black.

For example:

@codeblock{
 #lang racket
 (require 2htdp/image)
 (square 30 'solid 'puce)}

@;--------- PLATNUM ---------

@(shape-kata)  @bold{Platnium Shape Kata} or @bold{Not-Solid Kata}

Challenge the students to find the 1 other option that is the opposite
of @racket['solid] and code a shape featuring it.

@margin-note*{Teaching Tip: Put your students into teams to figure this
one out. Encourage super secrecy in order not to give away the answer to
 other teams!}

An example:

@codeblock{
 #lang racket
 (require 2htdp/image)
 (triangle 50 'outline 'purple)}




@;==== Combo Katas ====

@section{Combo Katas}

@margin-note*{Teaching Tip: Take time to review the ThoughtSTEM Core Values at the top of
 each class for the first several weeks.}


@; -------- BRONZE --------

@(combo-kata)  @bold{Bronze Combo Kata} or @bold{Beside Kata} (5 minutes)

@margin-note*{Teaching Tip: Test their previous knowledge and have the students
create two shapes before you show them how to beside.}

Within five minutes, the student can translate a sentence of this type:

@italic{Place two red circles beside eachother.}

To code of this type:

@codeblock{
 #lang racket
 (require 2htdp/image)

 (beside
  (circle 20 'solid 'red)
  (circle 20 'solid 'red))}

@;-------- SILVER ---------

@(combo-kata) @bold{Silver Combo Kata} or @bold{Above Kata} (5 minutes)

Within five minutes, the student can translate a sentence of this type:

@italic{Place two different sized triangles above one another.}

@margin-note*{Teaching Tip: Have students figure out how an above is different
than just coding two shapes in a row. (one image, centered, connected)}

To code of this type:

@codeblock{
 #lang racket
 (require 2htdp/image)

 (above
  (triangle 50 'solid 'blue)
  (triangle 80 'solid 'purple))}

@;--------- GOLD ----------

@(combo-kata) @bold{Gold Combo Kata} or @bold{Overlay Kata} (5 minutes)

Within five minutes, the student can translate a sentence of this type:

@italic{Overlay one square on top of a larger square of a different color.}

@margin-note*{Remember: order of the shapes is important!}

To code of this type:

@codeblock{
 #lang racket
 (require 2htdp/image)

 (overlay
  (square 50 'solid 'blue)
  (square 80 'solid 'purple))}


@;==== Picture Project Katas ====
@section{Picture Project Katas}

@;-------- BRONZE --------

@(picture-project) @bold{Bronze Piture Project Kata} or @bold{House Kata} (5 minutes)

Within five minutes, the student can build a simple house picture.

For example:

@codeblock{
#lang racket
(require 2htdp/image)

(above
 (triangle 100 'solid 'aqua)
 (square 100 'solid 'blue))}

@;-------- SILVER --------

@(picture-project) @bold{Silver Picture Project Kata} or @bold{Tree Kata} (5 minutes)

Within five minutes, the student can build a simple tree picture.

Example complete code:

@codeblock{
#lang racket
(require 2htdp/image)

(above
 (circle 30 'solid 'green)
 (rectangle 20 80 'solid 'brown))}

@;-------- GOLD --------

@(picture-project) @bold{Gold Picture Project Kata} or @bold{House Plus Kata} (10 minutes)

Within ten minutes, the student can build a simple house picture, adding a door.

@margin-note*{Teaching Tip: Spend some time messing around with overlay/align. Options for the
first parameter, "middle", also include "right" and "left." "bottom" can also be "middle" or "top"}

Example complete code:

@codeblock{
#lang racket
(require 2htdp/image)

(above
 (triangle 100 'solid 'aqua)
 
 (overlay/align "middle" "bottom"
                (rectangle 30 50 'solid 'brown)
                (square 100 'solid 'blue)))}

@;-------- PLATNIUM --------

@(picture-project) @bold{Platnium Picture Project Kata} or @bold{House Set Kata} (10 minutes)

Within ten minutes, the student can build a house with a door beside

@margin-note*{Teaching Tip: You may want to start with the complete code from the last kata,
then add the new code (focusing on relationships and parentheses), before you start building
 it all from scratch.}

Example complete code:

@codeblock{
#lang racket
(require 2htdp/image)

(beside/align "bottom"
 (above
  (triangle 100 'solid 'aqua)
 
  (overlay/align "middle" "bottom"            
                 (rectangle 30 50 'solid 'brown)
                 (square 100 'solid 'blue)))

 (above
  (circle 30 'solid 'green)
  (rectangle 20 80 'solid 'brown)))}